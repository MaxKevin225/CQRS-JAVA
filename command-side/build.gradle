buildscript {
    repositories {
        mavenCentral()
    }


    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.2.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'docker'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

jar {
    baseName = 'product-command-side'
}

dependencies {
    compile project(':common-things')
    compile("org.axonframework:axon-amqp:2.4.3")
    compile("org.axonframework:axon-mongo:2.4.3")
    compile("org.springframework.boot:spring-boot-starter-actuator:1.3.2.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-web:1.3.2.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-jetty:1.3.2.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-amqp:1.3.2.RELEASE")
    compile('org.springframework.boot:spring-boot-starter-data-mongodb:1.3.2.RELEASE')
    testCompile("org.springframework.boot:spring-boot-starter-test:1.3.2.RELEASE")
    testCompile("org.axonframework:axon-test:2.4.3")
    testCompile("com.jayway.restassured:rest-assured:2.8.0")
}

task buildDocker(type: Docker, dependsOn: assemble){
    push = false
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst{
        copy {
            from jar
            into stageDir
        }
    }
}

task createDistribution(type: Zip, dependsOn: buildDocker){
    from buildDocker.stageDir
    from('src/main/docker/Dockerfile')
    from('src/main/docker/Dockerrun.aws.json')
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

// http://www.petrikainulainen.net/programming/gradle/getting-started-with-gradle-integration-testing/
check.dependsOn integrationTest
integrationTest.mustRunAfter test